---
layout: bt_wiki
title: Classes Language Specification
category: Doc
draft: false
weight: 100
aliases: /doc/Classes-Language-Specification/
---

= Classes Language Specification

== Basics
A classes `package` is specified in a image:../../images/icons/Classes.png[] `.classes` file.
A `package` has a name and contains classes, interfaces, enums, and datatypes.

== Package
The package is defined at the beginning of the file with the following 
[source,dc43code]
----
package test.hello
----
The name is somehow important because it defines the namespace of all containing elements.
For this package a Class `ClassA` would have the full qualified name `test.hello.ClassA`
When using this class e.g. inside a *G+[]* create statement. Either the full qualified name has to be 
specified or a import statement imports the Class to be referenced by the simple class name.
[source,dc43code]
----
import test.hello.ClassB
...
G+[test.hello.ClassA]
G+[ClassB]
----
Wildcard imports are also possible. A `import test.hello.*` would import all elements inside this package.

== Class
A class is defined with:
[source,dc43code]
----
class ClassName {
}
----
=== Attribute
Classes can contain attributes that hold values. As example we define an attribute of type `String` with the name `desc` 
[source,dc43code]
----
desc : String
----
After the type a multiplicity can be set. For the example here we define a multi-valued attribute
of the type String. See the link:#DC43Classes-Multiplicity[multiplicity] chapter for detailed information.
[source,dc43code]
----
desc : String []
----
A optional default value can also be defined. 
[source,dc43code]
----
desc : String = "hello"
----

=== Reference
A reference defines a relationship between classifiers. It is defined with
[source,dc43code]
----
ClassifierName : referenceName
----
After the ClassifierName a multiplicity can be defined. See the link:#DC43Classes-Multiplicity[multiplicity] chapter for detailed information.
[source,dc43code]
----
ClassifierName [2..*] : referenceName
----
If you want to define a bi-directional reference that is navigable in both direction you can use the keyword `opposite`.
[source,dc43code]
----
ClassifierName : referenceName opposite otherReferenceName
----

=== Operation
A operation can be defined to specify behavior of a class. Inside the body of the operation you can use the DC43Code syntax.
[source,dc43code]
----
op operationName() {
    // body of the operation
}
----
Between the `()` brackets parameters can be defined the same as attributes
[source,dc43code]
----
op operationName(par1 : String, par2 : Double [*]) { }
---- 
The return type is defined after the `()` brackets. If non is given it is assumed that no return type is defined (`void`).
[source,dc43code]
----
op operationName() : String {
    return "hello"
}
----

=== Equations
Mathematical Equations can be defined inside a equation block.
[source,dc43code]
----
equations {
    attr1 == attr2 + 2
}
----
TODO write more here
[source,dc43code]
----
    attr1 == attr2 + 2 activeIf attr3 > 3
----

=== Conditions
Conditions for a class can be specified inside a conditions block. These conditions are checked in a validation step.
[source,dc43code]
----
conditions {
    attr1 > 0
}
---- 
Optionally you can specify a message that is shown if the condition is evaluated to false instead of a standard message.
[source,dc43code]
----
conditions {
    attr1 > 0 message = "The attribute attr1 must be greater than zero"
}
----

[[DC43Classes-Multiplicity]]
=== Multiplicity
A multiplicity is defined between `[]` brackets. It can define the multiplicity of attributes, references and parameters.
No multiplicity defined is the same as a `[0..1]` multiplicity. So the single-value can be either not set or set.
A `[]` multiplicity is the short-hand for `[0..*]` or `[*]`. This defines a multi-value which can hold any amount of values.
Furthermore you can also specify the lower or upper bound. E.g. `[2..4]`, the only valid amount of values would be between 2 and 4.

=== Derived Attributes
A derived attribute is not stored instead it is calculated. E.g.
[source,dc43code]
----
derived lastName : String get {
    val index  = name.lastIndexOf(' ')
    return if (index != -1) name.substring(index + 1) else name
}
----

== Interface
For an interface everything stated for class also applies. Though you can't create nodes of interfaces.

== Enum
A enum is short for https://en.wikipedia.org/wiki/Enumerated_type[Enumeration]  and is defined with
[source,dc43code]
----
enum EnumName {
}
----
The enum literals are recommended to be defined with uppercase.
[source,dc43code]
----
enum Cardsuit { CLUBS, DIAMONDS, SPADES, HEARTS }
----
If an attribute of the type Cardsuit is defined, only these literals can be assigned to it.

== Library example
[source,dc43code]
----
package test

class Library {
     name : String = "Test"
     pages : Integer
     Book bestBook
     Book[10..*] books opposite library

     op getBook(title:String) : Book { 
          for (Book book : books){
               if (title == book.title) return book
          }
          return null
     }
     // same behavior as getBook but more compact
     op getBook2(searchTitle:String) : Book {
          return books.findFirst[title == searchTitle]
     }

}

class Book {
     height : Unit[mm]
     width : Unit[mm]
     copyright : Date

     Library library opposite books

     equations {
      height == 2 + library.pages
     }
     conditions {
      height > 4
     }
}

enum BookCategory {
     MYSTERY
     SCIENCE_FICTION
     BIOGRAPHY
}

// makes the java type java.util.Date available
type Date wraps java.util.Date
----

== Advanced

=== Names
If a name should be the same as some reserved keyword, you can escape the name with a `^`.
E.g. if a attribute should be named `id`:
[source,dc43code]
----
^id : String
----

=== Enum literals
If you want to specify the integer values for the enum:
[source,dc43code]
----
enum BookCategory {
   Mystery = 0
   ScienceFiction = 1
   Biography = 2
}
----

If you want to specify the literal values for the enum:
[source,dc43code]
----
enum BookCategory {
   Mystery as "M"
   ScienceFiction as "S"
   Biography as "B"
}
----

If you want to specify both the integer value and the literal values for the enum:
[source,dc43code]
----
enum BookCategory {
   Mystery as "M" = 0
   ScienceFiction as "S" = 1
   Biography as "B" = 2
}
----


=== Datatype
You can use any Java Class as attribute type after importing it with following syntax.
[source,dc43code]
----
type Date wraps java.util.Date
----
If a graph is saved it must be serialized so it must be defined how this happens.
If you don't need the attribute to be serialized you can specifiy the 'transient' keyword before the attribute.
Otherwise you need to specify the create and convert methods.
[source,dc43code]
----
type Date wraps java.util.Date
create {
    try {
        if (it != null) new SimpleDateFormat("yyyy-MM-dd").parse(it)
    } catch (ParserException ex) {
        throw new RuntimeException(ex)
    }
}
convert {
    if(it != null) new SimpleDateFormat("yyyy-MM-dd").format(it)
}

----